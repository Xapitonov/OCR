plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
}

def getPropertyIfDefined(property, defaultValue) {
    if (project.hasProperty(property)) {
        return project.properties[property]
    }
    return defaultValue
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "org.totschnig.ocr"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 6
        versionName "2.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "engine"

    productFlavors {
        mlkit {
            dimension "engine"
            applicationIdSuffix ".mlkit"
        }
        tesseract {
            dimension "engine"
            applicationIdSuffix ".tesseract"
        }
    }

    signingConfigs {
        release {
            storeFile file(getPropertyIfDefined("RELEASE_STORE_FILE", "unknown"))
            storePassword getPropertyIfDefined("RELEASE_STORE_PASSWORD", "unknown")
            keyAlias getPropertyIfDefined("RELEASE_KEY_ALIAS", "unknown")
            keyPassword getPropertyIfDefined("RELEASE_KEY_PASSWORD", "unknown")

        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    testImplementation 'junit:junit:4.13.2'
    mlkitImplementation 'com.google.android.gms:play-services-mlkit-text-recognition:17.0.0'
    tesseractImplementation 'com.github.adaptech-cz:tesseract4android:3.0.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.activity:activity-ktx:1.3.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}